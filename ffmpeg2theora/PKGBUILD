# Maintainer: s8321414 <s8321414 at gmail.com>
# Arch Linux maintainers and contributors:
# Maintainer: Igor Isaias Banlian <igorisaiasbanlian[at]gmail[dot]com>
# Contributor: Michael HÃ¤sel <michael[dot]haesel[at]gmail[dot]com>
# Contributor: Daniel Aguayo <daniel[at]varpub[dot]org>
# Contributor: Jaroslav Lichtblau <dragonlord[at]aur[dot]archlinux[dot]org>
# Contributor: Eric Belanger <eric[at]archlinux[dot]org>
# Contributor: Angelo Theodorou <encelo[at]users[dot]sourceforge[dot]net>

pkgname=ffmpeg2theora
pkgver=0.30
pkgrel=1
pkgdesc="A simple converter to create Ogg Theora files."
arch=('x86_64')
url="http://www.v2v.cc/~j/ffmpeg2theora/"
license=('GPLv2')
depends=('ffmpeg')
makedepends=('scons')
source=(http://www.v2v.cc/~j/${pkgname}/downloads/${pkgname}-${pkgver}.tar.bz2
        'ffmpeg2theora-0.29-underlinking.patch')
sha256sums=('4f6464b444acab5d778e0a3359d836e0867a3dcec4ad8f1cdcf87cb711ccc6df'
            '3b937c7e3a5afe327ee75e246ef606a747b8fb5880b43a864b3b9db4a245ef7f')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  echo "#define AVCODEC_MAX_AUDIO_FRAME_SIZE 192000" >> src/ffmpeg2theora.h
  patch -p0 -i ../ffmpeg2theora-0.29-underlinking.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  scons
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  scons install destdir="${pkgdir}" prefix="/usr" mandir="PREFIX/share/man"
}
