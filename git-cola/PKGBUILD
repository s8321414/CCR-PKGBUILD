# Maintainer: Jeff Huang <s8321414[at]gmail[dot]com>
# Contributer: Renan Biegelmeyer (RenanBS) <renan.biegel[at]gmail[.dot]com>
# Arch Maintainer: TDY <tdy@gmx.com>

pkgname=git-cola
pkgver=2.8
pkgrel=1
pkgdesc="A powerful GUI for Git"
arch=('x86_64')
url=("http://git-cola.github.com/")
license=('GPL')
depends=('git' 'python3-pyqt5' 'icu')
makedepends=('asciidoc' 'docbook-xsl' 'python3-sphinx' 'rsync' 'xmlto')
optdepends=('python3-pyinotify: for inotify support'
            'tk: to use the built-in ssh-askpass handler')
options=('!makeflags')
source=("https://github.com/git-cola/git-cola/archive/v${pkgver}.zip")
md5sums=('fce5fd9223afdc885628e41e3ea990f5')

build(){
   cd "$srcdir/$pkgname-$pkgver"
   make all doc html
}

package(){
  cd "$srcdir/$pkgname-$pkgver"
  make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html} 
}
