# Maintainer: s8321414 <s8321414 at chakralinux.org> & Chakra Taiwan Community
# Contributor: Brli <rainman59118 at gmail.com> & Chakra Taiwan Community
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=gcin
pkgver=2.9.0
pkgrel=1
pkgdesc='Input method server supporting various input methods'
arch=('x86_64')
license=('LGPL')
url='https://hyperrate.com/dir.php?eid=67'
depends=('gtk3' 'libxtst' 'curl')
makedepends=('anthy' 'qt5-base' 'gtk2')
optdepends=('qt5-base: support for qt5 input method'
            'gtk2: support for gtk2 input method'
            'anthy: support for anthy input method')
source=("https://hyperrate.com/gcin-source/${pkgname}-${pkgver}.tar.xz"
	'fix-gtk-immodules-path.patch')
sha256sums=('8ae41fb35911112989a9388d66b678d142e05afe3e09063c9ef6fd5746127e60'
            '578c67fd1d405e7d6ee9910bbfd152fb940f577953c7ed844b28924bd786b883')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -Np1 -i ../fix-gtk-immodules-path.patch
  
  sed -e 's/lib64/lib/g' \
        -e "s/^OPTFLAGS=.*/OPTFLAGS=\"$CFLAGS $CPPFLAGS\"/" \
        -e "s/echo \"LDFLAGS=/echo \"LDFLAGS=$LDFLAGS /" \
        -e "s/echo \"SO_FLAGS=/echo \"SO_FLAGS=$LDFLAGS /" \
        -i configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --use_i18n=Y
  make -j1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
