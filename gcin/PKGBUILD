# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>
# Contributor: Brli <rainman59118 at gmail.com> & Chakra Taiwan Community
# Maintainer: s8321414 <s8321414 at chakralinux.org> & Chakra Taiwan Community

pkgname=gcin
pkgver=2.8.6
pkgrel=1
pkgdesc='Input method server supporting various input methods'
arch=('x86_64')
license=('LGPL')
url='http://hyperrate.com/dir.php?eid=67'
depends=('gtk2' 'libxtst' 'curl')
makedepends=('qt' 'anthy' 'qt5-base' 'gtk3')
optdepends=('qt: support for qt4 input method'
            'qt5-base: support for qt5 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method')
install=${pkgname}.install
source=("http://hyperrate.com/gcin-source/${pkgname}-${pkgver}.tar.xz"
	'qt4.patch'
	'gcin-fix-qt5-iid.patch'
	'gcin-improve-build-with-pkgconfig.patch'
        'gcin-dont-copy-gcin-qt5-header.patch')
sha1sums=('be6b3f2253976f1448788b2f7c4de43bb3d907c3'
          '8cb63a2e368aa43769532812bfc2c724327cfe53'
          '2559ef9d09cb917c0631d4762bb1b91c0f41aa5a'
          '737ba36743271288aa307c09eee590779fba17c4'
          '3a52dc43e28c5b0de5c19e955506089fb85a3ab4')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix Qt5 input plugins
  rm -f qt5-im/gcin-qt5.h.in
  
  patch -p1 -i ../qt4.patch
  patch -p1 -i ../gcin-fix-qt5-iid.patch
  patch -p1 -i ../gcin-improve-build-with-pkgconfig.patch
  patch -p1 -i ../gcin-dont-copy-gcin-qt5-header.patch
  
  sed 's/lib64/lib/g' -i configure
  sed '/include suffixes-rule/a \
        CFLAGS+='"${CFLAGS}"' \
        LDFLAGS+='"${LDFLAGS}"' \
        OPTFLAGS=' \
        -i Makefile
  
  sed \
        -e '/^MODVERSION=/a INCS+=-I/usr/include/qt5/QtGui/$(MODVERSION) -I/usr/include/qt5/QtCore/$(MODVERSION)' \
        -i qt5-im/Makefile
  
  # Qt 5.6 doesn't provide Qt5PlatformSupport.pc anymore
  sed -i 's/Qt5PlatformSupport//' qt5-im/Makefile
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --use_i18n=Y \
              --gcinlibdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mkdir -p ${pkgdir}/usr/lib/gtk-2.0/2.10.0
  mv "${pkgdir}/usr/lib/gtk-2.0/immodules" "${pkgdir}/usr/lib/gtk-2.0/2.10.0/immodules"
}
