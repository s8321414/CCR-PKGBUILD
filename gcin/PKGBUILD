# Maintainer: s8321414 <s8321414 at chakralinux.org> & Chakra Taiwan Community
# Contributor: Brli <rainman59118 at gmail.com> & Chakra Taiwan Community
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=gcin
pkgver=2.8.9
pkgrel=1
pkgdesc='Input method server supporting various input methods'
arch=('x86_64')
license=('LGPL')
url='https://hyperrate.com/dir.php?eid=67'
depends=('gtk3' 'libxtst' 'curl')
makedepends=('qt' 'anthy' 'qt5-base' 'gtk2')
optdepends=('qt: support for qt4 input method'
            'qt5-base: support for qt5 input method'
            'gtk2: support for gtk2 input method'
            'anthy: support for anthy input method')
source=("https://hyperrate.com/gcin-source/${pkgname}-${pkgver}.tar.xz"
	'qt4.patch'
	'fix-gtk-immodules-path.patch')
sha256sums=('e08426cc85c5932392ebf73ac7d140cd778862517c3ea0cf8953aaab6c14d078'
            'dbf9c32ac0059c62ea4628b277810c1b1c02e2ace98ae255f18c0bec0a348daa'
            '578c67fd1d405e7d6ee9910bbfd152fb940f577953c7ed844b28924bd786b883')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -p1 -i ../qt4.patch
  patch -Np1 -i ../fix-gtk-immodules-path.patch
  
  sed 's/lib64/lib/g' -i configure
  sed '/include suffixes-rule/a \
        CFLAGS+='"${CFLAGS}"' \
        LDFLAGS+='"${LDFLAGS}"' \
        OPTFLAGS=' \
        -i Makefile
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --use_i18n=Y
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
