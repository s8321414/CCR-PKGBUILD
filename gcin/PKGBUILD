#
# Chakra community repo packages, part of chakraos.org
#
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>
# Contributor: Brli <rainman59118 at gmail.com> & Chakra Taiwan Community
# Maintainer: s8321414 <s8321414 at gmail.com> & Chakra Taiwan Community

pkgname=gcin
pkgver=2.8.3
pkgrel=2
pkgdesc='Input method server supporting various input methods'
arch=('x86_64')
license=('LGPL')
url='http://hyperrate.com/dir.php?eid=67'
depends=('gtk2>=2.16.1' 'libxtst')
makedepends=('qt' 'anthy' 'qt5-base' 'gtk3')
optdepends=('qt: support for qt4 input method'
            'qt5-base: support for qt5 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method')
install=${pkgname}.install
source=("http://hyperrate.com/gcin-source/${pkgname}-${pkgver}.tar.xz"
	'qt4.patch'
	'libdir.patch'
	'qt5.patch')
sha1sums=('95b980f7cf9dc2ad91803113cdbb4ac831f056de'
          '8cb63a2e368aa43769532812bfc2c724327cfe53'
          'f8c7db8487ef2c8edd95e6f5af6d145460285675'
          'e4273dcf1ec78ff139c2c40c1735fb5f3690add6')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ../qt4.patch
  patch -p1 -i ../libdir.patch
  patch -p1 -i ../qt5.patch
  
  sed '/include suffixes-rule/a \
        CFLAGS+='"${CFLAGS}"' \
        LDFLAGS+='"${LDFLAGS}"' \
        OPTFLAGS=' \
        -i Makefile
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr \
              --use_i18n=Y \
              --gcinlibdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mkdir -p ${pkgdir}/usr/lib/gtk-2.0/2.10.0
  mv "${pkgdir}/usr/lib/gtk-2.0/immodules" "${pkgdir}/usr/lib/gtk-2.0/2.10.0/immodules"
}
