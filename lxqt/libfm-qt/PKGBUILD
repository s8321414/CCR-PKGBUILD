# Maintainer: s8321414 <s8321414@chakraos.org>
# Arch Linux maintainers and contributors:
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=libfm-qt
pkgver=0.11.2
pkgrel=1
pkgdesc="Core library of PCManFM-Qt (Qt binding for libfm)"
arch=("x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("GPL2")
depends=("libfm" "qt5-x11extras")
makedepends=("cmake" "qt5-tools" "lxqt-build-tools")
source=(https://downloads.lxqt.org/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('d6154760fd89eea9c4f87acce4c8c692852229ed8d5bbaf25f4a8517024f74fd')

build() {
	mkdir -p build
	cd build

	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
