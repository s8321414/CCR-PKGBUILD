# Maintainer: s8321414 <s8321414 at gmail.com>
# Arch Linux maintainers and contributors:
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=pcmanfm-qt
pkgver=0.10.0
pkgrel=1
pkgdesc="The LXQt file manager, Qt port of PCManFM."
arch=('x86_64')
url="http://lxqt.org"
license=('GPL2')
depends=("liblxqt" "libfm" "lxmenu-data")
makedepends=("cmake" "qt5-tools")
conflicts=('pcmanfm-qt-git')
source=(http://downloads.lxqt.org/lxqt/${pkgver}/${pkgname}-${pkgver}.tar.xz)
install=${pkgname}.install
sha256sums=('254c1da0d3fce2ab0a03b21861191b0875ce43382c74d3abbfa8f3dc88a080a4')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	mkdir -p build && cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib ..
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}/build"
	make DESTDIR="${pkgdir}" install
}
