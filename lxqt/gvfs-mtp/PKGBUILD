# Maintainer: s8321414 <s8321414 at gmail.com>
# Arch Linux maintainers and contributors:
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=('gvfs-mtp')
_pkgname=('gvfs')
pkgver=1.22.3
pkgrel=1
pkgdesc="A minimal build of gvfs with most backends disabled. Useful to mount your Windows Phone 8.1 through pcmanFM"
arch=('x86_64')
license=('LGPL')
depends=('udisks2' 'libmtp')
makedepends=('intltool' 'dbus-glib' 'docbook-xsl')
url="http://www.gnome.org"
conflicts=('gvfs')
provides=('gvfs')
option=(!libtool)
install=${pkgname}.install
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver:0:4}/$_pkgname-$pkgver.tar.xz)
sha256sums=('c778342e09a84d8c69b1b7ce7f943f6f2444a995bb0c0754ef1746afa8935182')

build() {
  cd $srcdir/$_pkgname-$pkgver
  
  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --localstatedir=/var \
	      --libexecdir=/usr/lib/gvfs \
	      --disable-static \
	      --disable-afc \
	      --disable-afp \
	      --disable-avahi \
	      --disable-bluray \
	      --disable-gdu \
	      --disable-gphoto2 \
	      --disable-hal \
	      --disable-http \
	      --disable-keyring \
	      --disable-obexftp \
	      --disable-samba \
	      --disable-cdda \
	      --disable-goa \
	      --disable-archive \
	      --disable-gtk \
	      --with-bash-completion-dir=/usr/share/bash-completion/completions 
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  sed -e 's/^am__append_4/#am__append_4/' \
	  -e 's/^am__append_5/#am__append_5/' \
	  -i monitor/Makefile
  make DESTDIR=$pkgdir install

  # Disable network automount
  sed -i 's/AutoMount=true/AutoMount=false/' $pkgdir/usr/share/gvfs/mounts/network.mount

  # Remove useless GConf dir
  rm -rf $pkgdir/usr/share/GConf
}
