# Maintainer: s8321414 <s8321414@chakraos.org>
# Arch Linux maintainers and contributors:
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-panel
pkgver=0.11.1
pkgrel=1
pkgdesc="The LXQt desktop panel"
arch=("x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("libdbusmenu-qt5" "libxcomposite" "lxmenu-data" "lxqt-common"
	 "lxqt-globalkeys" "menu-cache" "solid" "libpulse" "libstatgrab"
	 "libsysstat" "lm_sensors")
makedepends=("cmake" "qt5-tools" "lxqt-build-tools")
source=(https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz
        fix-build.patch)
sha256sums=('cfbe1862a2614ff8e22bc8db795a6cbce106243822d90370970eb0bbd5aea5e0'
            '994ad0a8873e1a190fb6890c19e5f65b863fc2b2faf1973ed27b3db31e9a9cfb')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -Np1 -i "$srcdir/fix-build.patch"
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	mkdir -p build && cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib ..
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}/build"
	make DESTDIR="${pkgdir}" install
}
