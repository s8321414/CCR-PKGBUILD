# Maintainer: s8321414 <s8321414 at gmail.com>
# Arch Linux maintainers and contributors:
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=libfm-extra
pkgver=1.2.5
pkgrel=1
pkgdesc='Extra library for file management' 
url='http://pcmanfm.sourceforge.net/'
arch=('x86_64')
license=('LGPL')
depends=('glib2')
makedepends=('intltool' 'udisks')
source=(http://downloads.sourceforge.net/pcmanfm/libfm-${pkgver}.tar.xz)
md5sums=('a1ba9ae5e920f38b647dd511edd6c807')

build() {
  cd ${srcdir}/libfm-${pkgver}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --without-gtk \
    --with-extra-only
  make
}

package() {
  cd ${srcdir}/libfm-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  rm -rf "$pkgdir"/usr/include/libfm
  mkdir -p "$pkgdir"/usr/include/libfm
   
  mv "$pkgdir"/usr/include/libfm-*/*.h "$pkgdir/usr/include/libfm"
  
  rm -rf "$pkgdir"/usr/include/libfm-*
}
