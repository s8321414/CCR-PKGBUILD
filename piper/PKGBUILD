# Contributions from Arch:
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=piper
pkgver=0.4
pkgrel=1
pkgdesc='GTK application to configure gaming mice'
arch=('any')
url='https://github.com/libratbag/piper'
license=('GPL')
depends=('python3' 'python3-gobject' 'libratbag>=0.9.900' 'python3-evdev' 'python3-lxml' 'python3-cairo')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('d62db7509348506cfb17588612cdf03877dbe0bca26904519eef9657e45bfb28')

prepare() {
  cd $pkgname-$pkgver

  # Remove install script
  # This is handled by pacman hooks
  sed -i "/meson.add_install_script('meson_install.sh')/d" meson.build
}

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  chakra-meson ..

  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
