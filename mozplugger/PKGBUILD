# Maintainer: s8321414 <s8321414 at gmail.com>
# Arch Linux maintainers and contributors:
# Maintainer: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=mozplugger
pkgver=2.1.6
pkgrel=1
pkgdesc="A Mozilla & Firefox multimedia plugin."
arch=('i686' 'x86_64')
url="http://mozplugger.mozdev.org"
license=('GPL')
depends=('m4' 'libx11')
backup=('etc/mozpluggerrc')
install=mozplugger.install
source=($url/files/mozplugger-$pkgver.tar.gz)
sha256sums=('294cf06ad37b8d89e57ee9c4dc9e7549fd1b0dcec9769171d65dad36099e5fef')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
  #--enable-always-xembed
  # uncomment the preceding line to get mozplugger running in Chromium
  # this will always use Xembed which kills keyboard focus

  make
}

package() {
  cd $pkgname-$pkgver
  # doesn't work anymore
  #make install root="$pkgdir"

  install -Dm644 mozpluggerrc "$pkgdir/etc/mozpluggerrc"
  install -d "$pkgdir/usr/bin/"
  install -m755 mozplugger-{helper,controller,linker,update} "$pkgdir/usr/bin/"
  install -Dm755 mozplugger.so "$pkgdir/usr/lib/mozilla/plugins/mozplugger.so"
  install -Dm644 mozplugger.7 "$pkgdir/usr/share/man/man7/mozplugger.7"
}
